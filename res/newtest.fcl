FUNCTION_BLOCK word_booster

VAR_INPUT
	link :      REAL;
	metaTag :   REAL;
	title :     REAL;
	h1_h3 :     REAL;
END_VAR

VAR_OUTPUT
	boost :     REAL;
END_VAR

FUZZIFY link
	TERM none   := (0, 1) (1, 0);
	TERM little := trian 0 1 2;
	TERM aver   := trian 1 2 3;
	TERM high   := (2, 0) (3, 1) (4, 1);
END_FUZZIFY

FUZZIFY metaTag
	TERM none   := (0, 1) (1, 0);
	TERM little := trian 0 1 2;
	TERM aver   := trian 1 2 3;
	TERM high   := (2, 0) (3, 1) (4, 1);
END_FUZZIFY

FUZZIFY title
	TERM none   := (0, 1) (1, 0);
	TERM little := trian 0 1 2;
	TERM aver   := trian 1 2 3;
	TERM high   := (2, 0) (3, 1) (4, 1);
END_FUZZIFY

FUZZIFY h1_h3
	TERM none   := (0, 1) (1, 0);
	TERM little := trian 0 1 3;
	TERM aver   := trian 2 4 6;
	TERM high   := (5, 0) (7, 1) (8, 1);
END_FUZZIFY

DEFUZZIFY boost
	TERM none   := 1;
	TERM quoter := 1.25;
	TERM half   := 1.5;
	TERM twice  := 2;
	TERM triple := 3;

	METHOD : COGs;
	DEFAULT := 1;
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;
	ACT : MIN;
	ACCU : MAX;

	RULE 1 :    IF   link     IS high     OR link     IS aver
	            OR   metaTag  IS high     OR metaTag  IS aver
	            THEN boost    IS triple
	            ;
	RULE 2 :    IF   link     IS little
                OR   metaTag  IS little
                OR   title    IS aver
                THEN boost    IS half
                ;
	RULE 3 :    IF   link     IS little
                AND  metaTag  IS little
                AND  title    IS little
                THEN boost    IS twice
                ;
	RULE 4 :    IF   link     IS none
                AND  metaTag  IS none
                AND  title    IS little
                AND  h1_h3    IS aver
                THEN boost    IS half
                ;
	RULE 5 :    IF   link     IS little
                OR   metaTag  IS little
                OR   title    IS little
                AND  h1_h3    IS high
                THEN boost    IS twice
                ;
	RULE 6 :    IF   link     IS none
                AND  metaTag  IS none
                AND  title    IS none
                AND  h1_h3    IS high
                THEN boost    IS quoter
                ;
END_RULEBLOCK

END_FUNCTION_BLOCK
